string(ASCII 27 Esc)
set(ANSI_RESET          "${Esc}[0m")
set(ANSI_BOLD           "${Esc}[1m")
set(ANSI_ITALIC         "${Esc}[3m")
set(ANSI_UNDERLINE      "${Esc}[4m")
set(ANSI_RED            "${Esc}[31m")
set(ANSI_GREEN          "${Esc}[32m")
set(ANSI_YELLOW         "${Esc}[33m")
set(ANSI_BLUE           "${Esc}[34m")
set(ANSI_MAGENTA        "${Esc}[35m")
set(ANSI_CYAN           "${Esc}[36m")
set(ANSI_BOLD_RED       "${Esc}[1;31m")
set(ANSI_BOLD_GREEN     "${Esc}[1;32m")
set(ANSI_BOLD_YELLOW    "${Esc}[1;33m")
set(ANSI_BOLD_BLUE      "${Esc}[1;34m")
set(ANSI_BOLD_MAGENTA   "${Esc}[1;35m")
set(ANSI_BOLD_CYAN      "${Esc}[1;36m")

function(log_ok)
    message(STATUS "[ ${ANSI_BOLD_GREEN}ok${ANSI_RESET} ] " ${ARGV})
endfunction()

function(log_warn)
    message(STATUS "[${ANSI_BOLD_YELLOW}warn${ANSI_RESET}] " ${ARGV})
endfunction()

function(log_err)
    message(STATUS "[${ANSI_BOLD_RED}err!${ANSI_RESET}] " ${ARGV})
    message(FATAL_ERROR ${ARGV})
endfunction()

function(log_info)
    message(STATUS "[${ANSI_BOLD}info${ANSI_RESET}] " ${ARGV})
endfunction()

function(log_newline)
    message(STATUS "       " ${ARGV})
endfunction()

function(log_debug)
    message(STATUS "[${ANSI_BOLD_CYAN}hint${ANSI_RESET}] " ${ARGV})
endfunction()

macro(target_resource_directories TARGET)
    foreach (RESOURCE_DIR ${ARGN})
        set(DEST "${CMAKE_CURRENT_BINARY_DIR}/${RESOURCE_DIR}")
        set(SRC "${CMAKE_CURRENT_SOURCE_DIR}/${RESOURCE_DIR}")
        add_custom_command(TARGET ${TARGET} POST_BUILD
                COMMAND ${CMAKE_COMMAND} -E create_symlink ${SRC} ${DEST})
    endforeach ()
endmacro()

# Experimental
if (CMAKE_MESSAGE_OVERRIDE)
    set(CMAKE_MESSAGE_TYPES FATAL_ERROR SEND_ERROR WARNING AUTHOR_WARNING
            DEPRECATION NOTICE STATUS VERBOSE DEBUG TRACE)
    function(message)
        list(GET ARGV 0 MessageType)
        list(REMOVE_AT ARGV 0)
        if (MessageType IN_LIST CMAKE_MESSAGE_TYPES)
            string(SUBSTRING ${MessageType} 0 4 MessageTag)
            set(MessageTag "[${ANSI_BOLD_BLUE}${MessageTag}${ANSI_RESET}]")
            _message(${MessageType} ${MessageTag} "${ARGV}")
        else ()
            set(MessageTag "[${ANSI_BOLD}NONE${ANSI_RESET}]")
            _message(${MessageTag} ${MessageType} "${ARGV}")
        endif()
    endfunction()
endif ()